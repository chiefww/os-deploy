
#Использовать cmdline
#Использовать logos

Перем Лог;
Перем ПутьКШаблонам;
Перем ИмяШаблонаСкрипта;
Перем ИмяШаблонаОбработки;

Процедура ВыполнитьСкрипт()
	
	Парсер = Новый ПарсерАргументовКоманднойСтроки;
	Парсер.ДобавитьИменованныйПараметр("-tpl-script");
	Парсер.ДобавитьИменованныйПараметр("-tpl-epf");
	
	ЗначенияАргументов = Парсер.Разобрать(АргументыКоманднойСтроки);
	
	Если ЗначениеЗаполнено(ЗначенияАргументов.Получить("-tpl-script")) Тогда
		СкопироватьШаблонСкрипта(ЗначенияАргументов.Получить("-tpl-script"));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначенияАргументов.Получить("-tpl-epf")) Тогда
		СкопироватьШаблонОбработки(ЗначенияАргументов.Получить("-tpl-epf"));
	КонецЕсли;
	
КонецПроцедуры

Процедура СкопироватьШаблонСкрипта(Знач ИмяФайла)
	
	Лог.Информация("Копирование шаблона скрипта");
	
	ИмяШаблона = ОбъединитьПути(ПутьКШаблонам, ИмяШаблонаСкрипта);
	
	СкопироватьШаблон(ИмяШаблона, ИмяФайла);
	
КонецПроцедуры

Процедура СкопироватьШаблонОбработки(Знач ИмяФайла)
	
	Лог.Информация("Копирование шаблона обработки");
	
	ИмяШаблона = ОбъединитьПути(ПутьКШаблонам, ИмяШаблонаОбработки);
	
	СкопироватьШаблон(ИмяШаблона, ИмяФайла);
	
КонецПроцедуры

Процедура СкопироватьШаблон(Знач ИмяШаблона, Знач ИмяФайла)
	
	ИмяФайла = ПолучитьИмяФайла(ИмяФайла, ИмяШаблона);
	
	Лог.Информация(ИмяФайла);
	
	КопироватьФайл(ИмяШаблона, ИмяФайла);
	
КонецПроцедуры

Функция ПолучитьИмяФайла(Знач ИмяФайла, Знач ИмяШаблона)
	
	Файл = Новый Файл(ИмяФайла);
	
	Если Файл.Существует() И Файл.ЭтоКаталог() Тогда
		ФайлШаблона = Новый Файл(ИмяШаблона);
		ИмяФайла = ОбъединитьПути(Файл.ПолноеИмя, ФайлШаблона.Имя);
	КонецЕсли;
	
	Файл = Новый Файл(ИмяФайла);
	
	Возврат Файл.ПолноеИмя;
	
КонецФункции

Лог = Логирование.ПолучитьЛог("oscript.app.os-deploy");

ПутьКШаблонам = ОбъединитьПути(ТекущийСценарий().Каталог, "templates");
ИмяШаблонаСкрипта = "СкриптШаблон.os";
ИмяШаблонаОбработки = "ДействияСБазойШаблон.epf";

ВыполнитьСкрипт();
