
//** @name РаботаСПочтой
//** @desc Модуль для работы с электронной почтой

#Использовать tmail

//* @desc Отправляет сообщение по электронной почте.
//* @param ОписаниеПочты {ОписаниеУчетнойЗаписиПочты} Объект описания почты, отправитель.
//* @param Получатель {Строка} Адрес получателя.
//* @param Тема {Строка}
//* @param ТекстСообщения {Строка}
Процедура ОтправитьСообщение(ОписаниеПочты, Получатель, Тема, ТекстСообщения) Экспорт
	
	УправлениеПочтой = ПолучитьУправлениеПочтой(ОписаниеПочты);
	
	Сообщение = УправлениеПочтой.СтруктураСообщения;
	Сообщение.АдресЭлектроннойПочтыПолучателя = Получатель;
	Сообщение.ТемаСообщения = Тема;
	Сообщение.ТипТекстаПочтовогоСообщения = "Строка";
	Сообщение.ТекстСообщения = ТекстСообщения;
	
	Если Не УправлениеПочтой.ОтправитьСообщение() Тогда
		ВызватьИсключение СтрШаблон("Не удалось отправить сообщение: %1", УправлениеПочтой.ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

//* @desc Отправляет сообщение с результатом работы скрипта по электронной почте.
//*     Результатом работы считается выведенный лог.
//* @param ОписаниеПочты {ОписаниеУчетнойЗаписиПочты} Объект описания почты, отправитель.
//* @param Получатель {Строка} Адрес получателя.
//* @param Тема {Строка}
//* @param ТекстСообщения {Строка} Этот текст будет помещен перед результатом работы (необязательный).
Процедура ОтправитьРезультатРаботы(ОписаниеПочты, Получатель, Тема, Знач ТекстСообщения = "") Экспорт
	
	Результат = ВыводЛога.ПолучитьВывод();
	
	Если ЗначениеЗаполнено(ТекстСообщения) Тогда
		ТекстСообщения = ТекстСообщения + Символы.ПС + Символы.ПС + Результат;
	Иначе
		ТекстСообщения = Результат;
	КонецЕсли;
	
	ОтправитьСообщение(ОписаниеПочты, Получатель, Тема, ТекстСообщения);
	
КонецПроцедуры

Функция ПолучитьУправлениеПочтой(ОписаниеПочты)
	
	УправлениеПочтой = Новый ТУправлениеЭлектроннойПочтой;
	ЗаполнитьЗначенияСвойств(УправлениеПочтой.УчетнаяЗаписьЭП, ОписаниеПочты);
	
	Возврат УправлениеПочтой;
	
КонецФункции
