
#Использовать tmail

Процедура ОтправитьСообщение(ОписаниеПочты, Получатель, Тема, ТекстСообщения) Экспорт
	
	УправлениеПочтой = ПолучитьУправлениеПочтой(ОписаниеПочты);
	
	Сообщение = УправлениеПочтой.СтруктураСообщения;
	Сообщение.АдресЭлектроннойПочтыПолучателя = Получатель;
	Сообщение.ТемаСообщения = Тема;
	Сообщение.ТипТекстаПочтовогоСообщения = "Строка";
	Сообщение.ТекстСообщения = ТекстСообщения;
	
	Если Не УправлениеПочтой.ОтправитьСообщение() Тогда
		ВызватьИсключение СтрШаблон("Не удалось отправить сообщение: %1", УправлениеПочтой.ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправитьРезультатРаботы(ОписаниеПочты, Получатель, Тема, Знач ТекстСообщения = "") Экспорт
	
	Результат = ВыводЛога.ПолучитьВывод();
	
	Если ЗначениеЗаполнено(ТекстСообщения) Тогда
		ТекстСообщения = ТекстСообщения + Символы.ПС + Символы.ПС + Результат;
	Иначе
		ТекстСообщения = Результат;
	КонецЕсли;
	
	ОтправитьСообщение(ОписаниеПочты, Получатель, Тема, ТекстСообщения);
	
КонецПроцедуры

Функция ПолучитьУправлениеПочтой(ОписаниеПочты)
	
	УправлениеПочтой = Новый ТУправлениеЭлектроннойПочтой;
	ЗаполнитьЗначенияСвойств(УправлениеПочтой.УчетнаяЗаписьЭП, ОписаниеПочты);
	
	Возврат УправлениеПочтой;
	
КонецФункции
