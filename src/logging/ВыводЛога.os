
//** @name ВыводЛога
//** @desc Модуль логирования

#Использовать logos
#Использовать tempfiles

Перем Лог;
Перем ИмяФайлаЖурнала;

//* @desc Сообщение уровня Отладка.
//* @param Сообщение {Строка} Текст сообщения. Агрументы Параметр1..9 подставляются в сообщение.
Процедура Отладка(Знач Сообщение,
		Знач Параметр1 = Неопределено, Знач Параметр2 = Неопределено, Знач Параметр3 = Неопределено,
		Знач Параметр4 = Неопределено, Знач Параметр5 = Неопределено, Знач Параметр6 = Неопределено,
		Знач Параметр7 = Неопределено, Знач Параметр8 = Неопределено, Знач Параметр9 = Неопределено) Экспорт
	
	Лог().Отладка(Сообщение, Параметр1, Параметр2, Параметр3, Параметр4,
		Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
		
КонецПроцедуры

//* @desc Сообщение уровня Информация.
//* @param Сообщение {Строка} Текст сообщения. Агрументы Параметр1..9 подставляются в сообщение.
Процедура Сообщение(Знач Сообщение,
		Знач Параметр1 = Неопределено, Знач Параметр2 = Неопределено, Знач Параметр3 = Неопределено,
		Знач Параметр4 = Неопределено, Знач Параметр5 = Неопределено, Знач Параметр6 = Неопределено,
		Знач Параметр7 = Неопределено, Знач Параметр8 = Неопределено, Знач Параметр9 = Неопределено) Экспорт
	
	Лог().Информация(Сообщение, Параметр1, Параметр2, Параметр3, Параметр4,
		Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
		
КонецПроцедуры

//* @desc Сообщение уровня Информация, используется желтый цвет консоли.
//* @param Сообщение {Строка} Текст сообщения. Агрументы Параметр1..9 подставляются в сообщение.
Процедура Информация(Знач Сообщение,
		Знач Параметр1 = Неопределено, Знач Параметр2 = Неопределено, Знач Параметр3 = Неопределено,
		Знач Параметр4 = Неопределено, Знач Параметр5 = Неопределено, Знач Параметр6 = Неопределено,
		Знач Параметр7 = Неопределено, Знач Параметр8 = Неопределено, Знач Параметр9 = Неопределено) Экспорт
	
	УстановитьЦвет(ЦветКонсоли.Желтый);
	
	Лог().Информация(Сообщение, Параметр1, Параметр2, Параметр3, Параметр4,
		Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
		
	СброситьЦвет();
	
КонецПроцедуры

//* @desc Сообщение уровня Информация, используется зеленый цвет консоли.
//* @param Сообщение {Строка} Текст сообщения. Агрументы Параметр1..9 подставляются в сообщение.
Процедура Успех(Знач Сообщение,
		Знач Параметр1 = Неопределено, Знач Параметр2 = Неопределено, Знач Параметр3 = Неопределено,
		Знач Параметр4 = Неопределено, Знач Параметр5 = Неопределено, Знач Параметр6 = Неопределено,
		Знач Параметр7 = Неопределено, Знач Параметр8 = Неопределено, Знач Параметр9 = Неопределено) Экспорт
	
	УстановитьЦвет(ЦветКонсоли.Зеленый);
	
	Лог().Информация(Сообщение, Параметр1, Параметр2, Параметр3, Параметр4,
		Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
		
	СброситьЦвет();
	
КонецПроцедуры

//* @desc Сообщение уровня Предупреждение, используется красный цвет консоли.
//* @param Сообщение {Строка} Текст сообщения. Агрументы Параметр1..9 подставляются в сообщение.
Процедура Предупреждение(Знач Сообщение,
		Знач Параметр1 = Неопределено, Знач Параметр2 = Неопределено, Знач Параметр3 = Неопределено,
		Знач Параметр4 = Неопределено, Знач Параметр5 = Неопределено, Знач Параметр6 = Неопределено,
		Знач Параметр7 = Неопределено, Знач Параметр8 = Неопределено, Знач Параметр9 = Неопределено) Экспорт
	
	УстановитьЦвет(ЦветКонсоли.Красный);
	
	Лог().Предупреждение(Сообщение, Параметр1, Параметр2, Параметр3, Параметр4,
		Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
		
	СброситьЦвет();
	
КонецПроцедуры

//* @desc Сообщение уровня Ошибка, используется красный цвет консоли.
//* @param Сообщение {Строка} Текст сообщения. Агрументы Параметр1..9 подставляются в сообщение.
Процедура Ошибка(Знач Сообщение,
		Знач Параметр1 = Неопределено, Знач Параметр2 = Неопределено, Знач Параметр3 = Неопределено,
		Знач Параметр4 = Неопределено, Знач Параметр5 = Неопределено, Знач Параметр6 = Неопределено,
		Знач Параметр7 = Неопределено, Знач Параметр8 = Неопределено, Знач Параметр9 = Неопределено) Экспорт
	
	УстановитьЦвет(ЦветКонсоли.Красный);
	
	Лог().Ошибка(Сообщение, Параметр1, Параметр2, Параметр3, Параметр4,
		Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
		
	СброситьЦвет();
	
КонецПроцедуры

//* @desc Сообщение уровня КритическаяОшибка, используется красный цвет консоли.
//* @param Сообщение {Строка} Текст сообщения. Агрументы Параметр1..9 подставляются в сообщение.
Процедура КритическаяОшибка(Знач Сообщение,
		Знач Параметр1 = Неопределено, Знач Параметр2 = Неопределено, Знач Параметр3 = Неопределено,
		Знач Параметр4 = Неопределено, Знач Параметр5 = Неопределено, Знач Параметр6 = Неопределено,
		Знач Параметр7 = Неопределено, Знач Параметр8 = Неопределено, Знач Параметр9 = Неопределено) Экспорт
	
	УстановитьЦвет(ЦветКонсоли.Красный);
	
	Лог().КритическаяОшибка(Сообщение, Параметр1, Параметр2, Параметр3, Параметр4,
		Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
		
	СброситьЦвет();
	
КонецПроцедуры

//* @desc Возвращает текст всего выведенного лога.
//* @return {Строка}
Функция ПолучитьВывод() Экспорт
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ИмяФайлаЖурнала, "utf-8");
	Результат = ТекстовыйДокумент.ПолучитьТекст();
	
	Возврат Результат;
	
КонецФункции

//* @desc Закрывает временный файл лога, удаляет его.
Процедура Закрыть() Экспорт
	
	Лог.Закрыть();
	ВременныеФайлы.УдалитьФайл(ИмяФайлаЖурнала);
	
КонецПроцедуры

Процедура УстановитьЦвет(Цвет)
	
	Консоль = Новый Консоль;
	Консоль.ЦветТекста = Цвет;
	
КонецПроцедуры

Процедура СброситьЦвет()
	
	УстановитьЦвет(ЦветКонсоли.Серый);
	
КонецПроцедуры

Функция Лог()
	
	Если Лог = Неопределено Тогда
		Лог = Логирование.ПолучитьЛог("oscript.lib.os-deploy");
		
		ВыводВКонсоль = Новый ВыводЛогаВКонсоль;
		Лог.ДобавитьСпособВывода(ВыводВКонсоль);
		
		ИмяФайлаЖурнала = ВременныеФайлы.НовоеИмяФайла("log");
		ВыводВФайл = Новый ВыводЛогаВФайл;
		ВыводВФайл.ОткрытьФайл(ИмяФайлаЖурнала);
		Лог.ДобавитьСпособВывода(ВыводВФайл);
		
		Раскладка = Новый РаскладкаСообщенияЛога;
		Лог.УстановитьРаскладку(Раскладка);
	КонецЕсли;
	
	Возврат Лог;
	
КонецФункции
